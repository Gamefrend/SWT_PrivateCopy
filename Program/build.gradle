plugins {
    id 'application'
    id "org.openjfx.javafxplugin" version "0.1.0"
}


application {
    mainClass = "de.hsrm.mi.swt.app.StorageShelvesMain"
}

java {
    sourceCompatibility = '21'
}

javafx {
    version = "21.0.3"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// Initialisierung f√ºr JUnit-Testing
test {
    useJUnitPlatform()
}

/* ./gradlew fatJar erzeugt Standalone-JAR (App incl. benoetigter Bibliotheken)
 * Datei wird in Ordner build/libs angelegt
 * Dateiname ist letzte Komponente von application.mainClass (s.o.) + Endung ".jar"
 * Ausfuehrung mit java -jar <dateiname>
 * Mehr Infos: https://www.baeldung.com/gradle-fat-jar
 */
task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
    archiveBaseName = application.mainClass.get().split('\\.').last()

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

